plugins {
    id 'java'
}

group = 'org.gradle'
version = '4.3.1'
archivesBaseName = 'gradle-resources-http'

sourceCompatibility = targetCompatibility = 1.8

repositories {
    jcenter()
    maven { url 'https://maven.aliyun.com/repository/public/' }
    maven { url 'https://repo.gradle.org/gradle/libs-releases-local/' }
}

configurations {
    shade_core_api
    compile.extendsFrom(shade_core_api)
    shade_resources_http
    compile.extendsFrom(shade_resources_http)
}

dependencies {
    shade_core_api files('lib/gradle-core-api-' + version + '.jar')
    compile files('lib/gradle-base-services-' + version + '.jar')
    compile files('lib/gradle-resources-' + version + '.jar')
    compile files('lib/groovy-all-2.4.12.jar')
    shade_resources_http files('lib/gradle-resources-http-' + version + '.jar')
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.4.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.10'
    compile group: 'com.google.guava', name: 'guava', version: '17.0'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '1.3.9'
}

jar.enabled(false)

task core_api(type: Jar,) {
    baseName = 'gradle-core-api'
    from sourceSets.main.output
    configurations.shade_core_api.copyRecursive().setTransitive(false).each {
        artifact ->
            from(zipTree(artifact).matching {
                exclude '**/api/invocation/Gradle*'
            })
    }
    includeEmptyDirs = false
    exclude 'org/gradle/internal/'
}

task resources_http(type: Jar) {
    baseName = 'gradle-resources-http'
    from sourceSets.main.output
    configurations.shade_resources_http.copyRecursive().setTransitive(false).each {
        artifact ->
            from(zipTree(artifact).matching {
                exclude '**/http/HttpClientHelper*'
            })
    }
    includeEmptyDirs = false
    exclude 'org/gradle/api/'
}

build {
    dependsOn(core_api, resources_http)
}
